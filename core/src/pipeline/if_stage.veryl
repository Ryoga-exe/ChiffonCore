import eei::*;
import pipeline::if_fifo_type;

module if_stage (
    clk: input clock,
    rst: input reset,

    control_hazard        : input logic,
    control_hazard_pc_next: input Addr ,

    fifo_rready: input  logic       ,
    fifo_rvalid: output logic       ,
    fifo_rdata : output if_fifo_type,

    i_membus: modport membus_if::<ILEN, XLEN>::master,

    // debug
    debug_pc          : output Addr ,
    debug_is_requested: output logic,
    debug_pc_requested: output Addr ,
) {
    var pc          : Addr ; // program counter
    var is_requested: logic; // fetch 中かどうか
    var pc_requested: Addr ; // 要求アドレス

    let pc_next: Addr = pc + 4;

    // Debug
    always_comb {
        debug_pc           = pc;
        debug_is_requested = is_requested;
        debug_pc_requested = pc_requested;
    }

    // fetch
    always_comb {
        // FIFO に 2 個以上空きがあるとき fetch
        i_membus.valid = fifo_wready_two;
        i_membus.addr  = pc;
        i_membus.wen   = 0;
        i_membus.wdata = 'x; // no use
    }

    always_ff {
        if_reset {
            pc           = 0;
            is_requested = 0;
            pc_requested = 0;
        } else {
            if control_hazard {
                pc           = control_hazard_pc_next;
                is_requested = 0;
            } else {
                if is_requested {
                    if i_membus.rvalid {
                        is_requested = i_membus.ready && i_membus.valid;
                        if i_membus.ready && i_membus.valid {
                            pc           = pc_next;
                            pc_requested = pc;
                        }
                    }
                } else {
                    if i_membus.ready && i_membus.valid {
                        is_requested = 1;
                        pc           = pc_next;
                        pc_requested = pc;
                    }
                }
            }
        }
    }

    // ====================================================
    // * FIFO (IF -> ID)
    // ====================================================

    var fifo_wready    : logic       ;
    var fifo_wready_two: logic       ;
    var fifo_wvalid    : logic       ;
    var fifo_wdata     : if_fifo_type;

    inst if_fifo: fifo #(
        DATA_TYPE: if_fifo_type,
        WIDTH    : 3           ,
    ) (
        clk                        ,
        rst                        ,
        flush     : control_hazard ,
        wready    : fifo_wready    ,
        wready_two: fifo_wready_two,
        wvalid    : fifo_wvalid    ,
        wdata     : fifo_wdata     ,
        rready    : fifo_rready    ,
        rvalid    : fifo_rvalid    ,
        rdata     : fifo_rdata     ,
    );

    // IF FIFO
    always_ff {
        if_reset {
            fifo_wvalid = 0;
            fifo_wdata  = 0;
        } else {
            if control_hazard {
                fifo_wvalid = 0;
            } else {
                if is_requested && i_membus.rvalid {
                    fifo_wvalid     = 1;
                    fifo_wdata.addr = pc_requested;
                    fifo_wdata.bits = i_membus.rdata;
                } else {
                    if fifo_wvalid && fifo_wready {
                        // FIFO にデータを格納できる
                        fifo_wvalid = 0;
                    }
                }
            }
        }
    }
}
