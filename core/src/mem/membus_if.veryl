/// Memory bus interface
interface membus_if::<DATA_WIDTH: u32, ADDR_WIDTH: u32> {
    var valid : logic                ; /// Whether requesting memory access
    var ready : logic                ; /// Whether accepting memory access request
    var addr  : logic<ADDR_WIDTH>    ; /// Target access address
    var wen   : logic                ; /// Is write? (1: write)
    var wdata : logic<DATA_WIDTH>    ; /// Data to write
    var wmask : logic<DATA_WIDTH / 8>; /// Write mask (in bytes)
    var rvalid: logic                ; /// Is accepted request processing done?
    var rdata : logic<DATA_WIDTH>    ; /// Result of accepted read command

    modport master {
        valid       : output,
        ready       : input ,
        addr        : output,
        wen         : output,
        wdata       : output,
        wmask       : output,
        rvalid      : input ,
        rdata       : input ,
        wmask_expand: import,
    }

    modport slave {
        wmask_expand: import,
        ..converse(master)
    }

    // get DATA_WIDTH-bit expanded wmask
    function wmask_expand () -> logic<DATA_WIDTH> {
        var result: logic<DATA_WIDTH>;
        for i: u32               in 0..DATA_WIDTH {
            result[i] = wmask[i / 8];
        }
        return result;
    }
}

alias interface Membus = membus_if::<eei::MEMBUS_DATA_WIDTH, eei::XLEN>;
