import eei::*;
import corectrl::*;

/// CSR module
module csrunit (
    clk        : input  clock            ,
    rst        : input  reset            ,
    valid      : input  logic            , /// Whether a command/instruction is being supplied
    pc         : input  Addr             , /// Current Instruction Address
    ctrl       : input  InstCtrl         , /// The instruction's `InstCtrl`
    expt_info  : input  ExceptionInfo    , /// Exception Info
    rd_addr    : input  logic        <5> , /// Current instruction's rd number
    csr_addr   : input  logic        <12>, /// CSR address specified by the instruction (upper 12 bits of the instruction)
    rs1        : input  UIntX            , /// `rs1` when CSRR(W|S|C), the immediate (5 bits) zero-extended when CSRR(W|S|C)I
    rdata      : output UIntX            , /// Result of reading the CSR by the CSR instruction
    raise_trap : output logic            , /// 1 when raise trap
    trap_vector: output Addr             , /// Address to jump to when an exception occurs
    led        : output UIntX            , // LED
) {
    // CSRR(W|S|C)[I] inst?
    let is_wsc: logic = ctrl.is_csr && ctrl.funct3[1:0] != 0;
    // MRET inst?
    let is_mret: logic = ctrl.is_csr && csr_addr == 12'b0011000_00010 && rs1[4:0] == 0 && ctrl.funct3 == 0 && rd_addr == 0;

    // wmasks
    const MTVEC_WMASK : UIntX = 'hffff_ffff_ffff_fffc;
    const MEPC_WMASK  : UIntX = 'hffff_ffff_ffff_fffc;
    const MCAUSE_WMASK: UIntX = 'hffff_ffff_ffff_ffff;
    const LED_WMASK   : UIntX = 'hffff_ffff_ffff_ffff;

    // CSR
    var mtvec : UIntX;
    var mepc  : UIntX;
    var mcause: UIntX;

    var wmask: UIntX; // write mask
    var wdata: UIntX; // write data

    always_comb {
        // read
        rdata = case csr_addr {
            CsrAddr::MTVEC : mtvec,
            CsrAddr::MEPC  : mepc,
            CsrAddr::MCAUSE: mcause,
            CsrAddr::LED   : led,
            default        : 'x,
        };
        // write
        wmask = case csr_addr {
            CsrAddr::MTVEC : MTVEC_WMASK,
            CsrAddr::MEPC  : MEPC_WMASK,
            CsrAddr::MCAUSE: MCAUSE_WMASK,
            CsrAddr::LED   : LED_WMASK,
            default        : 0,
        };
        wdata = case ctrl.funct3[1:0] {
            2'b01  : rs1,
            2'b10  : rdata | rs1,
            2'b11  : rdata & ~rs1,
            default: 'x,
        } & wmask | (rdata & ~wmask);
    }

    always_ff {
        if_reset {
            mtvec  = 0;
            mepc   = 0;
            mcause = 0;
            led    = 0;
        } else {
            if valid {
                if raise_trap {
                    if raise_expt {
                        mepc   = pc;
                        mcause = trap_cause;
                    }
                } else {
                    if is_wsc {
                        case csr_addr {
                            CsrAddr::MTVEC : mtvec  = wdata;
                            CsrAddr::MEPC  : mepc   = wdata;
                            CsrAddr::MCAUSE: mcause = wdata;
                            CsrAddr::LED   : led    = wdata;
                            default        : {}
                        }
                    }
                }

            }
        }
    }

    // Exception
    let raise_expt: logic = valid && expt_info.valid;
    let expt_cause: UIntX = expt_info.cause;

    // Trap
    assign raise_trap  = raise_expt || (valid && is_mret);
    let trap_cause : UIntX = expt_cause;
    assign trap_vector = if raise_expt ? mtvec : mepc;
}
