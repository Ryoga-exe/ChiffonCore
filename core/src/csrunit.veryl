import eei::*;
import corectrl::*;

/// CSR module
module csrunit (
    clk     : input  clock       ,
    rst     : input  reset       ,
    valid   : input  logic       , /// Whether a command/instruction is being supplied
    ctrl    : input  InstCtrl    , /// The instruction's `InstCtrl`
    csr_addr: input  logic   <12>, /// CSR address specified by the instruction (upper 12 bits of the instruction)
    rs1     : input  UIntX       , /// `rs1` when CSRR(W|S|C), the immediate (5 bits) zero-extended when CSRR(W|S|C)I
    rdata   : output UIntX       , /// Result of reading the CSR by the CSR instruction
) {
    // CSRR(W|S|C)[I] inst?
    let is_wsc: logic = ctrl.is_csr && ctrl.funct3[1:0] != 0;

    // wmasks
    const MTVEC_WMASK: UIntX = 'hffff_fffc;

    // CSR
    var mtvec: UIntX;

    var wmask: UIntX; // write mask
    var wdata: UIntX; // write data

    always_comb {
        // read
        rdata = case csr_addr {
            CsrAddr::MTVEC: mtvec,
            default       : 'x,
        };
        // write
        wmask = case csr_addr {
            CsrAddr::MTVEC: MTVEC_WMASK,
            default       : 0,
        };
        wdata = case ctrl.funct3[1:0] {
            2'b01  : rs1,
            2'b10  : rdata | rs1,
            2'b11  : rdata & ~rs1,
            default: 'x,
        } & wmask | (rdata & ~wmask);
    }

    always_ff {
        if_reset {
            mtvec = 0;
        } else {
            if valid {
                if is_wsc {
                    case csr_addr {
                        CsrAddr::MTVEC: mtvec = wdata;
                        default       : {}
                    }
                }
            }
        }
    }
}
