import eei::*;
import corectrl::*;

module core (
    clk     : input   clock                                    ,
    rst     : input   reset                                    ,
    i_membus: modport membus_if::<ILEN, XLEN>::master          ,
    d_membus: modport membus_if::<MEM_DATA_WIDTH, XLEN>::master,
) {
    // ====================================================
    // * FIFO
    // ====================================================
    struct if_fifo_type {
        addr: Addr,
        bits: Inst,
    }

    var if_fifo_wready    : logic       ;
    var if_fifo_wready_two: logic       ;
    var if_fifo_wvalid    : logic       ;
    var if_fifo_wdata     : if_fifo_type;
    var if_fifo_rready    : logic       ;
    var if_fifo_rvalid    : logic       ;
    var if_fifo_rdata     : if_fifo_type;

    inst if_fifo: fifo #(
        DATA_TYPE: if_fifo_type,
        WIDTH    : 3           ,
    ) (
        clk                           ,
        rst                           ,
        wready    : if_fifo_wready    ,
        wready_two: if_fifo_wready_two,
        wvalid    : if_fifo_wvalid    ,
        wdata     : if_fifo_wdata     ,
        rready    : if_fifo_rready    ,
        rvalid    : if_fifo_rvalid    ,
        rdata     : if_fifo_rdata     ,
    );

    always_ff {
        if_reset {
            if_fifo_wvalid = 0;
            if_fifo_wdata  = 0;
        } else {
            if if_is_requested && i_membus.rvalid {
                if_fifo_wvalid     = 1;
                if_fifo_wdata.addr = if_pc_requested;
                if_fifo_wdata.bits = i_membus.rdata;
            } else {
                if if_fifo_wvalid && if_fifo_wready {
                    // FIFO にデータを格納できる
                    if_fifo_wvalid = 0;
                }
            }
        }
    }

    // ====================================================
    // * Instruction Fetch
    // ====================================================
    var if_pc          : Addr ; // program counter
    var if_is_requested: logic; // fetch 中か
    var if_pc_requested: Addr ; // 要求アドレス

    let if_pc_next: Addr = if_pc + 4;

    always_comb {
        // FIFO に 2 個以上空きがあるとき fetch
        i_membus.valid = if_fifo_wready_two;
        i_membus.addr  = if_pc;
        i_membus.wen   = 0;
        i_membus.wdata = 'x; // no use

        if_fifo_rready = !memu_stall; // memunit が処理中でないとき、FIFO から命令を取り出す
    }

    always_ff {
        if_reset {
            if_pc           = 0;
            if_is_requested = 0;
            if_pc_requested = 0;
        } else {
            if if_is_requested {
                if i_membus.rvalid {
                    if_is_requested = i_membus.ready && i_membus.valid;
                    if i_membus.ready && i_membus.valid {
                        if_pc           = if_pc_next;
                        if_pc_requested = if_pc;
                    }
                }
            } else {
                if i_membus.ready && i_membus.valid {
                    if_is_requested = 1;
                    if_pc           = if_pc_next;
                    if_pc_requested = if_pc;
                }
            }
        }
    }

    // ====================================================
    // * Instruction Decoder
    // ====================================================
    let inst_valid : logic    = if_fifo_rvalid;
    let inst_pc    : Addr     = if_fifo_rdata.addr;
    let inst_bits  : Inst     = if_fifo_rdata.bits;
    var inst_is_new: logic   ; // Whether the instruction was issued in the current clock cycle
    var inst_ctrl  : InstCtrl;
    var inst_imm   : UIntX   ;

    inst decoder: inst_decoder (
        bits: inst_bits,
        ctrl: inst_ctrl,
        imm : inst_imm ,
    );

    always_ff {
        if_reset {
            inst_is_new = 0;
        } else {
            inst_is_new = if if_fifo_rvalid ? if_fifo_rready : 1;
        }
    }

    // ====================================================
    // * Registers
    // ====================================================
    var regfile: UIntX<32>;

    // register number
    let rs1_addr: logic<5> = inst_bits[19:15];
    let rs2_addr: logic<5> = inst_bits[24:20];

    // source register data
    let rs1_data: UIntX = if rs1_addr == 0 ? 0 : regfile[rs1_addr];
    let rs2_data: UIntX = if rs2_addr == 0 ? 0 : regfile[rs2_addr];

    let rd_addr: logic<5> = inst_bits[11:7];
    let wb_data: UIntX    = switch {
        inst_ctrl.is_lui : inst_imm,
        inst_ctrl.is_load: memu_rdata,
        default          : alu_result,
    };

    always_ff {
        // write back
        if inst_valid && if_fifo_rready && inst_ctrl.rwb_en {
            regfile[rd_addr] = wb_data;
        }
    }

    // ====================================================
    // * ALU
    // ====================================================
    var op1       : UIntX;
    var op2       : UIntX;
    var alu_result: UIntX;

    always_comb {
        case inst_ctrl.itype {
            InstType::R, InstType::B: {
                op1 = rs1_data;
                op2 = rs2_data;
            }
            InstType::I, InstType::S: {
                op1 = rs1_data;
                op2 = inst_imm;
            }
            InstType::U, InstType::J: {
                op1 = inst_pc;
                op2 = inst_imm;
            }
            default: {
                op1 = 'x;
                op2 = 'x;
            }
        }
    }

    inst alum: alu (
        ctrl  : inst_ctrl ,
        op1               ,
        op2               ,
        result: alu_result,
    );

    // ====================================================
    // * Memory
    // ====================================================
    var memu_rdata: UIntX;
    var memu_stall: logic;

    inst memu: memunit (
        clk                ,
        rst                ,
        valid : inst_valid ,
        is_new: inst_is_new,
        ctrl  : inst_ctrl  ,
        addr  : alu_result ,
        rs2   : rs2_data   ,
        rdata : memu_rdata ,
        stall : memu_stall ,
        membus: d_membus   ,
    );

    var clock_count: u64;

    always_ff {
        if_reset {
            clock_count = 1;
        } else {
            clock_count += 1;

            if inst_valid {
                $display("# %d", clock_count);
                $display("%h : %h", inst_pc, inst_bits);
                $display("  itype   : %b", inst_ctrl.itype);
                $display("  imm     : %h", inst_imm);
                $display("  rs1[%d] : %h", rs1_addr, rs1_data);
                $display("  rs2[%d] : %h", rs2_addr, rs2_data);
                $display("  op1     : %h", op1);
                $display("  op2     : %h", op2);
                $display("  alu_res : %h", alu_result);
                $display("  mem stall : %b", memu_stall);
                $display("  mem rdata : %h", memu_rdata);
                if if_fifo_rready && inst_ctrl.rwb_en {
                    $display("  reg[%d] <= %h", rd_addr, wb_data);
                }
            }
        }
    }
}
