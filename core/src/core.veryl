import eei::*;

module core (
    clk   : input   clock                          ,
    rst   : input   reset                          ,
    membus: modport membus_if::<ILEN, XLEN>::master,
) {
    // FIFO
    struct if_fifo_type {
        addr: Addr,
        bits: Inst,
    }

    var if_fifo_wready    : logic       ;
    var if_fifo_wready_two: logic       ;
    var if_fifo_wvalid    : logic       ;
    var if_fifo_wdata     : if_fifo_type;
    var if_fifo_rready    : logic       ;
    var if_fifo_rvalid    : logic       ;
    var if_fifo_rdata     : if_fifo_type;

    inst if_fifo: fifo #(
        DATA_TYPE: if_fifo_type,
        WIDTH    : 3           ,
    ) (
        clk                           ,
        rst                           ,
        wready    : if_fifo_wready    ,
        wready_two: if_fifo_wready_two,
        wvalid    : if_fifo_wvalid    ,
        wdata     : if_fifo_wdata     ,
        rready    : if_fifo_rready    ,
        rvalid    : if_fifo_rvalid    ,
        rdata     : if_fifo_rdata     ,
    );

    var if_pc          : Addr ; // program counter
    var if_is_requested: logic; // fetch 中か
    var if_pc_requested: Addr ; // 要求アドレス

    let if_pc_next: Addr = if_pc + 4;

    always_comb {
        // FIFO に 2 個以上空きがあるとき fetch
        membus.valid = if_fifo_wready_two;
        membus.addr  = if_pc;
        membus.wen   = 0;
        membus.wdata = 'x; // no use

        if_fifo_rready = 1; // 常に FIFO から命令を受け取る
    }

    always_ff {
        if_reset {
            if_pc           = 0;
            if_is_requested = 0;
            if_pc_requested = 0;
        } else {
            if if_is_requested {
                if membus.rvalid {
                    if_is_requested = membus.ready && membus.valid;
                    if membus.ready && membus.valid {
                        if_pc           = if_pc_next;
                        if_pc_requested = if_pc;
                    }
                }
            } else {
                if membus.ready && membus.valid {
                    if_is_requested = 1;
                    if_pc           = if_pc_next;
                    if_pc_requested = if_pc;
                }
            }
        }
    }

    // FIFO
    always_ff {
        if_reset {
            if_fifo_wvalid = 0;
            if_fifo_wdata  = 0;
        } else {
            if if_is_requested && membus.rvalid {
                if_fifo_wvalid     = 1;
                if_fifo_wdata.addr = if_pc_requested;
                if_fifo_wdata.bits = membus.rdata;
            } else {
                if if_fifo_wvalid && if_fifo_wready {
                    // FIFO にデータを格納できる
                    if_fifo_wvalid = 0;
                }
            }
        }
    }

    // Debug
    let inst_pc  : Addr = if_fifo_rdata.addr;
    let inst_bits: Inst = if_fifo_rdata.bits;

    always_ff {
        if if_fifo_rvalid {
            $display("%h : %h", inst_pc, inst_bits);
        }
    }
}
